//=================================================================================
/*/{Protheus.doc} zViaCep
[API.VIACEP] [GET] Conecta na Api gratuita da ViaCep para retornar dados de um Endereço
a partir do CEP.
Documentação: https://viacep.com.br/
Campos Disponiveis na API:  
BAIRRO, CEP, COMPLEMENTO, GIA, IBGE, LOCALIDADE, LOGRADOURO, UF, UNIDADE
    
Uso: Incluir/Atualizar CC2 quando necessário
    
@author     Thiago.Andrrade
@since      24/07/2020
/*/
//=================================================================================
User Function BuscarCep()
    Local aArea         := GetArea()
    Local aHeader       := {}
    Local cCep          := ''
    Local cMensagem     := ''
    Local cCodMun       := ''
    Local CRLF := Chr(13) + Chr(10)
    Local oRestClient   := FWRest():New("https://viacep.com.br/ws")
    Local oJsObj

    cCep                := FwInputBox("Digite aqui seu CEP:", cCep)
    
    aadd(aHeader,'User-Agent: Mozilla/4.0 (compatible; Protheus '+GetBuild()+')')
    aAdd(aHeader,'Content-Type: application/json; charset=utf-8')
          
    ////////////////////////////////////////////////////////////////
    //[GET] Consulta Dados na Api
    oRestClient:setPath("/"+cCep+"/json/")
    If oRestClient:Get(aHeader)
        //Deserealiza o Json
        FWJsonDeserialize(oRestClient:CRESULT,@oJsObj)
    
        //Recebe Dados do Json
        cCodMun      := SubStr(oJsObj:IBGE,3,5)
        cBairro      := oJsObj:BAIRRO
        cComplemento := oJsObj:COMPLEMENTO
        cLocalidade  := oJsObj:LOCALIDADE
        cLogradouro  := oJsObj:LOGRADOURO
        cUf          := oJsObj:UF

        cMensagem       := "Rua: "+cLogradouro +CRLF
        cMensagem       += "Complemento: "+cComplemento +CRLF
        cMensagem       += "Cidade: "+cLocalidade +CRLF
        cMensagem       += "UF: "+cUf +CRLF

        MsgInfo(cMensagem, "endereço do CEP "+cCep+":")
    
        FreeObj(oJsObj)
    Else
        MsgInfo(oRestClient:GetLastError(),"Erro Api ViaCep: ")
    Endif
    
    RestArea(aArea)
Return
